function(gris_get_dependent_libraries var_name target)
  math(EXPR _argc_mod2 "${ARGC} % 2")
  if(_argc_mod2 )
    message(FATAL_ERROR [=[gris_get_dependent_libraries must have an even number of arguments. 
    interface is: 
    gris_get_dependent_libraries(output_variable target_name [target_property_name target_property_value [...]] 
    ]=])
  endif()
  
  set(result )
  # fill _link_libraries with dependent libraries
  get_target_property(_type ${target} TYPE)
  get_target_property(_link_libraries ${target} INTERFACE_LINK_LIBRARIES)
  if(NOT _link_libraries)
    set(_link_libraries )
  endif()
  if(NOT ${_type} MATCHES "^INTERFACE_")
    get_target_property(_tmp_link_libraries ${target} LINK_LIBRARIES)
    if(_tmp_link_libraries)
      set(_link_libraries ${_link_libraries} ${_tmp_link_libraries})
    endif()
  endif()
  
  
  foreach(_lib ${_link_libraries})
    if(TARGET ${_lib})
      gris_get_dependent_libraries(_tmp ${_lib} ${ARGN})
      if(_tmp)
        set(result ${result} ${_tmp})
      endif()
    endif()
  endforeach()
  
  set(_keep ON)
  if(${ARGC} GREATER 2)
    foreach(_loop RANGE 3 ${ARGC} 2)
      math(EXPR _loop1 "${_loop}-1")
      get_target_property(_var ${target} ${ARGV${_loop1}})
      if(NOT _var OR NOT ${_var} STREQUAL ${ARGV${_loop}})
        set(_keep OFF)
      endif()
    endforeach()
  endif()
  if(${_keep})
    set(result ${result} ${target})
  endif()
  if(result)
    list(REMOVE_DUPLICATES result)
  endif()
  set(${var_name} ${result} PARENT_SCOPE)
endfunction()
