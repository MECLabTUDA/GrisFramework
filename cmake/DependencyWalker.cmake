function(gris_get_dependent_libraries var_name target)
# gris_get_dependent_libraries looks at the targets defined in cmake (imported or not) starting with 
# target. Target properties, that have to be fulfilled by the dependent library target can also be specified.
#
# ARGUMENTS
# gris_get_dependent_libraries(output_variable target [property_name value [...]])

  if(DEBUG_CMAKE)
    set(_debug ON)
  else()
    set(_debug OFF)
  endif()
  if(_debug)
    message("target ${target}")
  endif()
  math(EXPR _argc_mod2 "${ARGC} % 2")
  if(_argc_mod2 )
    message(FATAL_ERROR [=[gris_get_dependent_libraries must have an even number of arguments. 
    interface is: 
    gris_get_dependent_libraries(output_variable target_name [target_property_name target_property_value [...]] 
    ]=])
  endif()
  set(all_libraries )
  _gris_get_dependent_libraries_rec(all_libraries ${target})
  if(_debug)
    message("all dependent libs: ${all_libraries}")
  endif()
  set(result )
  foreach(_lib ${all_libraries})
    set(_keep ON)
    if(${ARGC} GREATER 2)
      foreach(_loop RANGE 3 ${ARGC} 2)
        math(EXPR _loop1 "${_loop}-1")
        get_target_property(_var ${_lib} ${ARGV${_loop1}})
        if(NOT _var OR NOT ${_var} STREQUAL ${ARGV${_loop}})
          set(_keep OFF)
          if(_debug)
            message("dropping ${_lib}, because ${_var} not ${ARGV${_loop}}")
          endif()
        endif()
      endforeach()
    endif()
    if(${_keep})
      set(result ${result} ${_lib})
    endif()
  endforeach()
  if(result)
    list(REMOVE_DUPLICATES result)
  endif()
  if(_debug)
    message("filtered libs: ${result}")
  endif()
  set(${var_name} ${result} PARENT_SCOPE)
endfunction()
  
  
function(_gris_get_dependent_libraries_rec var_name target)
  # fill _link_libraries with dependent libraries
  get_target_property(_type ${target} TYPE)
  get_target_property(_link_libraries ${target} INTERFACE_LINK_LIBRARIES)
  if(NOT _link_libraries)
    set(_link_libraries )
  endif()
  if(NOT ${_type} MATCHES "^INTERFACE_")
    get_target_property(_tmp_link_libraries ${target} LINK_LIBRARIES)
    if(_tmp_link_libraries)
      set(_link_libraries ${_link_libraries} ${_tmp_link_libraries})
    endif()
  endif()
  set(_super_list ${${var_name}})
  foreach(_lib ${_link_libraries})
    if(TARGET ${_lib} AND NOT "${_lib}" IN_LIST _super_list)
      set(_super_list ${_super_list} ${_lib})
      _gris_get_dependent_libraries_rec(_super_list ${_lib})
    endif()
  endforeach()
  set(${var_name} ${_super_list} PARENT_SCOPE)
endfunction()